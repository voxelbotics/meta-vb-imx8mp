# We have a conf and classes directory, add to BBPATH
# Note how we PREpend BBPATH. This is due to the fact that
# bitbake does not override classes per layer as it does
# recipes. Classes are processed strictly in BBPATH order.
# As we unfortunately have to override standard classes for
# bug fixing, we prepend instead of append our path.

#This layer is a stripped down version of the AMP meta-alb layer
#meant to be used independently outside the AMP solution.
BBPATH =. "${LAYERDIR}:"

BBFILE_COLLECTIONS += "hovergames-distro"
BBFILE_PATTERN_hovergames-distro := "^${LAYERDIR}"
BBFILE_PRIORITY_hovergames-distro = "8"

BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bbappend' % layer \
		for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bb' % layer \
		for layer in BBFILE_COLLECTIONS.split())}"

BBFILES += " \
        ${LAYERDIR}/images/*.bb* \
        ${LAYERDIR}/packagegroups/*.bb* \
        ${LAYERDIR}/recipes-*/*/*.bb \
        ${LAYERDIR}/recipes-*/*/*.bbappend \
        ${LAYERDIR}/classes/*.bb* \
"


LAYERSERIES_COMPAT_hovergames-distro = "mickledore"

FSL_BIN_MIRROR ?= "${FSL_MIRROR}"
